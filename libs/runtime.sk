;;  The Stańczyk Programming Language  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;                                                                      ;
;            ¿«fº"└└-.`└└*∞▄_              ╓▄∞╙╙└└└╙╙*▄▄               ;
;         J^. ,▄▄▄▄▄▄_      └▀████▄ç    JA▀            └▀v             ;
;       ,┘ ▄████████████▄¿     ▀██████▄▀└      ╓▄██████▄¿ "▄_          ;
;      ,─╓██▀└└└╙▀█████████      ▀████╘      ▄████████████_`██▄        ;
;     ;"▄█└      ,██████████-     ▐█▀      ▄███████▀▀J█████▄▐▀██▄      ;
;     ▌█▀      _▄█▀▀█████████      █      ▄██████▌▄▀╙     ▀█▐▄,▀██▄    ;
;    ▐▄▀     A└-▀▌  █████████      ║     J███████▀         ▐▌▌╙█µ▀█▄   ;
;  A╙└▀█∩   [    █  █████████      ▌     ███████H          J██ç ▀▄╙█_  ;
; █    ▐▌    ▀▄▄▀  J█████████      H    ████████          █    █  ▀▄▌  ;
;  ▀▄▄█▀.          █████████▌           ████████          █ç__▄▀ ╓▀└╙%_;
;                 ▐█████████      ▐    J████████▌          .└╙   █¿  ,▌;
;                 █████████▀╙╙█▌└▐█╙└██▀▀████████                 ╙▀▀▀ ;
;                ▐██▀┘Å▀▄A └▓█╓▐█▄▄██▄J▀@└▐▄Å▌▀██▌                     ;
;                █▄▌▄█M╨╙└└-           .└└▀**▀█▄,▌                     ;
;                ²▀█▄▄L_                  _J▄▄▄█▀└                     ;
;                     └╙▀▀▀▀▀MMMR████▀▀▀▀▀▀▀└                          ;
;                                                                      ;
;                                                                      ;
; ███████╗████████╗ █████╗ ███╗   ██╗ ██████╗███████╗██╗   ██╗██╗  ██╗ ;
; ██╔════╝╚══██╔══╝██╔══██╗████╗  ██║██╔════╝╚══███╔╝╚██╗ ██╔╝██║ ██╔╝ ;
; ███████╗   ██║   ███████║██╔██╗ ██║██║       ███╔╝  ╚████╔╝ █████╔╝  ;
; ╚════██║   ██║   ██╔══██║██║╚██╗██║██║      ███╔╝    ╚██╔╝  ██╔═██╗  ;
; ███████║   ██║   ██║  ██║██║ ╚████║╚██████╗███████╗   ██║   ██║  ██╗ ;
; ╚══════╝   ╚═╝   ╚═╝  ╚═╝╚═╝  ╚═══╝ ╚═════╝╚══════╝   ╚═╝   ╚═╝  ╚═╝ ;
;                                                                      ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  The Stańczyk Programming Language  ;;

;; The Stańczyk Runtime Library
; Note: The Runtime library is automatically loaded into any program created
; with Stańczyk by the compiler. Some definitions and documentation provided
; are also defined within the compiler itself.

;;; Compiler-Defined Constants

;; Operating System
; bool OS_LINUX   - True if program is running on Linux.
; bool OS_MAC     - True if program is running on Mac.
; bool OS_WINDOWS - True if program is running on Windows.

;; Stańczyk Internals
; bool SK_DEBUG   - True if program is compiled with `-debug` switch.
; bool SK_TEST    - True if program is running in test mode (`skc test`)
; str  SK_VERSION - The Stańczyk Compiler version (`skc version`)

;; Maximum Values for Signed Integers (int).
const MAX_INT8     127
const MAX_INT16    32767
const MAX_INT32    2147483647
const MAX_INT64    9223372036854775807

;; Minimum Values for Signed Integers (int).
; const MIN_INT8     -128
; const MIN_INT16    -32768
; const MIN_INT32    -2147483648
; const MIN_INT64    -9223372036854775808

;; Maximum Values for Unsigned Integers (uint).
const MAX_UINT8  255
const MAX_UINT16 65535
const MAX_UINT32 4294967295
const MAX_UINT64 18446744073709551615

; Helper function to increment a number or a pointer position by 1.
; Calling:
;   - <int/pointer> ++
; Returns:
;   - The given argument + 1
fn ++ $X -> X (
	1 +
)

; Helper function to decrement a number or a pointer position by 1.
; Calling:
;   - <int/pointer> --
; Returns:
;   - The given argument - 1.
fn -- $X -> X (
	1 -
)

; Helper function to get a result of a division.
; Note: Assembly's `DIV` instruction returns both the result
; and the modulo. This function drops the modulo and returns
; only the result.
; Calling:
;   - <int1> <int2> /
; Returns:
;   - The result of <int1> / <int2>.
fn / int int -> int (
	div drop
)

; Helper function to get the modulo of a division.
; Note: Assembly's `DIV` instruction returns both the result
; and the modulo. This function drops the result and returns
; only the modulo.
; Calling:
;   - <int1> <int2> %
; Returns:
;   - The result of <int1> % <int2>.
fn % int int -> int (
	div swap drop
)

; Removes one value from the stack
; Calling
;   - <any value> drop
fn drop any (
	bind ( x )
)

; Removes two values from the stack
; Calling
;   - <any> <any> 2drop
fn 2drop any any (
	bind ( x y )
)

; Duplicate the last value on the stack
; Calling:
;   - <any> dup
; Returns:
;   - The given argument, twice
fn dup $X -> X X (
	bind ( x ) x x
)

; Duplicate the last 2 values on the stack
; Calling:
;   - <any> <any> 2dup
; Returns:
;   - The 2 given arguments, twice
fn 2dup $X $Y -> X Y X Y (
	bind ( x y ) x y x y
)

; Helper function for Logical And operator (&&).
; Calling:
;   - <bool1> <bool2> and
; Returns:
;   - A boolean value. `true` if both given values are true, `false` if one or
;     more are false.
fn and bool bool -> bool (
	swap (int)
	swap (int)
	+ 2 =
)

; Helper function for Logical Or operator (||).
; Calling:
;   - <bool1> <bool2> or
; Returns:
;   - A boolean value. `true` if at least of the given values is true.
fn or bool bool -> bool (
	swap (int)
	swap (int)
	+ 0 >
)

; Helper function to calculate the difference between 2 pointers.
; Calling:
;   - <pointer1> <pointer2> ptrdiff
; Returns:
;   - <int> with the difference between the given pointers.
fn ptrdiff ptr ptr -> int (
	swap (int)
	swap (int)
	-
)
