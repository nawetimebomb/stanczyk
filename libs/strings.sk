;;  The Stańczyk Programming Language  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;                                                                      ;
;            ¿«fº"└└-.`└└*∞▄_              ╓▄∞╙╙└└└╙╙*▄▄               ;
;         J^. ,▄▄▄▄▄▄_      └▀████▄ç    JA▀            └▀v             ;
;       ,┘ ▄████████████▄¿     ▀██████▄▀└      ╓▄██████▄¿ "▄_          ;
;      ,─╓██▀└└└╙▀█████████      ▀████╘      ▄████████████_`██▄        ;
;     ;"▄█└      ,██████████-     ▐█▀      ▄███████▀▀J█████▄▐▀██▄      ;
;     ▌█▀      _▄█▀▀█████████      █      ▄██████▌▄▀╙     ▀█▐▄,▀██▄    ;
;    ▐▄▀     A└-▀▌  █████████      ║     J███████▀         ▐▌▌╙█µ▀█▄   ;
;  A╙└▀█∩   [    █  █████████      ▌     ███████H          J██ç ▀▄╙█_  ;
; █    ▐▌    ▀▄▄▀  J█████████      H    ████████          █    █  ▀▄▌  ;
;  ▀▄▄█▀.          █████████▌           ████████          █ç__▄▀ ╓▀└╙%_;
;                 ▐█████████      ▐    J████████▌          .└╙   █¿  ,▌;
;                 █████████▀╙╙█▌└▐█╙└██▀▀████████                 ╙▀▀▀ ;
;                ▐██▀┘Å▀▄A └▓█╓▐█▄▄██▄J▀@└▐▄Å▌▀██▌                     ;
;                █▄▌▄█M╨╙└└-           .└└▀**▀█▄,▌                     ;
;                ²▀█▄▄L_                  _J▄▄▄█▀└                     ;
;                     └╙▀▀▀▀▀MMMR████▀▀▀▀▀▀▀└                          ;
;                                                                      ;
;                                                                      ;
; ███████╗████████╗ █████╗ ███╗   ██╗ ██████╗███████╗██╗   ██╗██╗  ██╗ ;
; ██╔════╝╚══██╔══╝██╔══██╗████╗  ██║██╔════╝╚══███╔╝╚██╗ ██╔╝██║ ██╔╝ ;
; ███████╗   ██║   ███████║██╔██╗ ██║██║       ███╔╝  ╚████╔╝ █████╔╝  ;
; ╚════██║   ██║   ██╔══██║██║╚██╗██║██║      ███╔╝    ╚██╔╝  ██╔═██╗  ;
; ███████║   ██║   ██║  ██║██║ ╚████║╚██████╗███████╗   ██║   ██║  ██╗ ;
; ╚══════╝   ╚═╝   ╚═╝  ╚═╝╚═╝  ╚═══╝ ╚═════╝╚══════╝   ╚═╝   ╚═╝  ╚═╝ ;
;                                                                      ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  The Stańczyk Programming Language  ;;

;; The Stańczyk Strings Library

; Calculates the length of a number (as in number of characters)
; Calling:
;   - <int> len
; Returns:
;   - The number of characters. If the number is 13, it will return 2
fn len (int --- int) 0 dup rot < until ++ I 10 / loop ret

; Calculates the length of a string (as in number of characters)
; Calling:
;   - <string> len
; Returns:
;   - The number of characters in teh string. "Stanczyk" returns 8
fn len (ptr --- int) let s in 0 0 dup s c@ != until ++ dup s c@ loop done ret

; Compares two strings and returns the results of the comparison
; Calling:
;   - <str1> <str2> strequal
; Returns:
;   - A boolean true if the arguments are equal, false if they are different.
fn strequal (ptr ptr --- bool)
  over len swap dup len
  let s1 l1 s2 l2 in
    l1 l2 = dup if
      0 l1 <= until
        I s1 c@ I s2 c@ != if false nip leave fi
      I -- loop
    fi
  done
ret

; Converts an integer to a string and returns the buffer where the result is saved
; Calling:
;   - <int> itoa
; Returns:
;   - The pointer to the static memory where the string is saved
const ITOA_BUFFER_CAP 8 ; TODO: ptr sizeof
var itoa-buffer ptr

fn itoa (int --- ptr)
  let x in
    ITOA_BUFFER_CAP 0 >= until
      I '\0' itoa-buffer c!
    I ++ loop

    x len 0 = if
      0 '0' itoa-buffer c!
    fi

    x
    0 x len 1 - <= until
      dup 10 % 48 + char I swap itoa-buffer c!
      take 10 /
    I -- loop
    drop
  done

  itoa-buffer
ret
